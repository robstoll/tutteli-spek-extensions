buildscript {
    project.group = 'ch.tutteli.spek'
    project.version ='0.4.0'
    project.description = 'A set of Spek extensions'

    ext {
        // project setup
        tutteli_gradle_version = '0.26.1'

        // main
        kotlin_version = '1.3.41'

        // test
        junit_platform_version = '1.2.0'
        spek_version = '2.0.4'
        atrium_version = '0.8.0'

        // detekt/sonarcloud
        detekt_version = '1.0.0.RC8'
    }

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_platform_version"
        classpath "ch.tutteli:tutteli-gradle-dokka:$tutteli_gradle_version"
        classpath "ch.tutteli:tutteli-gradle-junitjacoco:$tutteli_gradle_version"
        classpath "ch.tutteli:tutteli-gradle-kotlin-module-info:$tutteli_gradle_version"
        classpath "ch.tutteli:tutteli-gradle-kotlin-utils:$tutteli_gradle_version"
        classpath "ch.tutteli:tutteli-gradle-publish:$tutteli_gradle_version"
        classpath "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
}

repositories {
    jcenter()
    maven { url "https://dl.bintray.com/spekframework/spek" }
}

apply plugin: 'kotlin'

apply plugin: 'ch.tutteli.dokka'

apply plugin: 'ch.tutteli.kotlin.utils'
kotlinutils.kotlinVersion = kotlin_version
apply plugin: 'ch.tutteli.kotlin.module.info'

apply plugin: 'ch.tutteli.junitjacoco'

apply plugin: 'ch.tutteli.publish'

apply plugin: "io.gitlab.arturbosch.detekt"
detekt {
    version = detekt_version
    defaultProfile {
        filters = ".*/resources/.*,.*$rootProject.name/.*/build/.*"
        output = "$rootProject.buildDir/detekt"
        config = file("$rootProject.projectDir/gradle/scripts/detekt.yml")
    }
}

dependencies {
    implementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version", excludeKotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version", excludeKotlin
    testImplementation "ch.tutteli.atrium:atrium-cc-en_GB-robstoll:$atrium_version", excludeKotlin
}

junitPlatform {
    filters {
        engines {
            include 'spek2'
        }
    }
}

/*

Release & deploy a commit
--------------------------------
1. change search for X.Y.Z-SNAPSHOT and change to X.Y.Z
2. search for current version and replace with new
3. update master:
    a) point to the tag
        1) update badge versions
        2) search for `tree/master` and replace it with `tree/vX.Y.Z` (README.md)
    b) commit (modified build.gradle, README.md)
    c) git tag vX.Y.Z
    d) git push origin vX.Y.Z
4. deploy to bintray:
    a) ./gr clean publishToBintray
    b) Log in to bintray, check and publish new jars
5. create release on github

Prepare next dev cycle
-----------------------
1. change version in build.gradle to X.Y.Z-SNAPSHOT
2. point to master
   a) search for `tag=vX.Y.Z` and replace it with `branch=master`
   b) search for `tree/vX.Y.Z` and replace it with `tree/master`
3. commit & push changes

*/
